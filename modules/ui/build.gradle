version '2.0.0-SNAPSHOT'

processResources {

    def expandedFiles = ['config/application.properties']

    from ('src/main/resources') {
        include expandedFiles
        expand(project.properties)
    }
    from ('src/main/resources') {
        exclude expandedFiles
    }

    into 'build/main/resources'

}

ext {
    codecVersion = '1.11'
    jdomVersion = '2.0.6'
}

dependencies {

    compileOnly project(':jworkspace')

    compile "commons-codec:commons-codec:$codecVersion"
    compile "org.jdom:jdom2:$jdomVersion"
}

jar {
    manifest {
         attributes([
                 "PluginName": "$archiveBaseName",
                 "PluginType": "ANY",
                 "PluginDescription": "$archiveClassifier",
                 "PluginIcon": "",
                 "PluginVersion": "$archiveVersion",
                 "PluginHelpURL": ""
         ], "jworkspace.ui.WorkspaceGUI.class")
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task deployToPlugins(type: Copy) {
    from jar
    into "${System.getProperty("user.home")}/.jworkspace/plugins/"
}

deployToPlugins.dependsOn(assemble)
build.finalizedBy deployToPlugins