version '2.0.0-SNAPSHOT'

processResources {

    def expandedFiles = ['config/application.properties']

    from ('src/main/resources') {
        include expandedFiles
        expand(project.properties)
    }
    from ('src/main/resources') {
        exclude expandedFiles
    }

    into 'build/main/resources'

}

ext {
    codecVersion = '1.11'
    jdomVersion = '2.0.6'
    jodaVersion = '2.10.3'
}

dependencies {

    compileOnly project(':jworkspace')
    compileOnly project(':ui')
    testCompile project(':ui')

    compileOnly "org.jdom:jdom2:$jdomVersion"
    compile "joda-time:joda-time:$jodaVersion"
}

jar {
    manifest {
         attributes([
                 "PluginName": jar.archiveBaseName.get(),
                 "PluginType": "ANY",
                 "PluginDescription": jar.archiveClassifier.get(),
                 "PluginIcon": "",
                 "PluginVersion": jar.archiveVersion.get(),
                 "PluginHelpURL": ""
         ], "jworkspace.ui.runtime.RuntimeManagerWindow.class")
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task deployToPlugins(type: Copy) {
    from jar
    into "${System.getProperty("user.home")}/.jworkspace/users/default/shells/"
}

deployToPlugins.dependsOn(assemble)
build.finalizedBy deployToPlugins